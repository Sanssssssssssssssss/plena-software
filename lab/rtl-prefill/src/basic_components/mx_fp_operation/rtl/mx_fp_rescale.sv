`timescale 1ns / 1ps

/*
Module      : MX-FP Rescale Unit
Timing      : Sequential, Takes 1 cycles to rescale
Description : e1s1, e2s2, e3s3, e4s4 - > {e1, e2, e3, e4} s
            : Format a block of elements, rescale them to have the same scale (max(scales))
Status      : Passed Simple Tests
            : Note that the output scale is the maximum scale of the input elements.
            : The output element is in MX-FP format.
*/

module mx_fp_rescale #(
    // MX-FP Data Format
    parameter IN_MXFP_EXP_WIDTH     = 4,
    parameter IN_MXFP_MANT_WIDTH    = 3,
    parameter MXFP_SCALE_WIDTH      = 8,

    // Dimension
    parameter   BLOCK_DIM           = 4,

    // MX-FP Data Format
    parameter OUT_MXFP_EXP_WIDTH    = 4,
    parameter OUT_MXFP_MANT_WIDTH   = 3

) (
    input logic clk,
    input logic rst,

    // Input matrix
    input  logic [BLOCK_DIM - 1 : 0] [IN_MXFP_MANT_WIDTH + IN_MXFP_EXP_WIDTH : 0] element_in,
    input  logic [BLOCK_DIM - 1 : 0] [MXFP_SCALE_WIDTH - 1 : 0]             scale_in,

    output logic [BLOCK_DIM - 1 : 0] [OUT_MXFP_MANT_WIDTH + OUT_MXFP_EXP_WIDTH : 0] element_data_out,
    output logic [MXFP_SCALE_WIDTH - 1 : 0] scale_data_out
);

    logic [BLOCK_DIM - 1 : 0] [OUT_MXFP_MANT_WIDTH + OUT_MXFP_EXP_WIDTH : 0] p0_rounded_element, p1_rounded_element;
    logic [BLOCK_DIM - 1 : 0] [MXFP_SCALE_WIDTH - 1 : 0] p0_rounded_scale, p1_rounded_scale;
    

    generate;
        if (IN_MXFP_EXP_WIDTH != OUT_MXFP_EXP_WIDTH || IN_MXFP_MANT_WIDTH != OUT_MXFP_MANT_WIDTH) begin : round_element
            mx_fp_element_round #(
                .IN_EXP_WIDTH   (IN_MXFP_EXP_WIDTH),
                .IN_MANT_WIDTH  (IN_MXFP_MANT_WIDTH),
                .SCALE_WIDTH    (MXFP_SCALE_WIDTH),
                .OUT_EXP_WIDTH  (OUT_MXFP_EXP_WIDTH),
                .OUT_MANT_WIDTH (OUT_MXFP_MANT_WIDTH)
            ) mxfp_round (
                .data_in        (element_in),
                .scale_in       (scale_in),
                .data_out       (p0_rounded_element),
                .scale_out      (p0_rounded_scale)
            );
        end else begin : no_round
            assign p0_rounded_element   = element_in;
            assign p0_rounded_scale     = scale_in;
        end

    endgenerate

    logic [MXFP_SCALE_WIDTH - 1 : 0] exp_max;

    unsigned_max #(
        .width(MXFP_SCALE_WIDTH),
        .length(BLOCK_DIM),
        .flop_output(0)
    ) u0_exp_max (
        .clk(clk),
        .input_data(p0_rounded_scale),
        .max_val(exp_max)
    );

    always @(posedge clk) begin
        if (rst) begin
            p1_rounded_element <= 'b0;
            p1_rounded_scale <= 'b0;
        end else begin
            p1_rounded_element  <= p0_rounded_element;
            p1_rounded_scale    <= p0_rounded_scale;
        end
    end


    generate;
        for (genvar i = 0; i < BLOCK_DIM; i++) begin : gen_rescale
            logic signed [OUT_MXFP_EXP_WIDTH : 0] exp_reduce_amount, new_element_exp;
            always_comb begin
                exp_reduce_amount = $signed({1'b0, exp_max}) - $signed({1'b0, p1_rounded_scale[i]});
                if (exp_reduce_amount < 0) begin
                    exp_reduce_amount = 0;
                end
                new_element_exp = $signed({1'b0, p1_rounded_element[i][OUT_MXFP_MANT_WIDTH + OUT_MXFP_EXP_WIDTH - 1 : OUT_MXFP_MANT_WIDTH]}) - exp_reduce_amount;
                if (new_element_exp < 0) begin
                    element_data_out[i] = {
                        p1_rounded_element[i][OUT_MXFP_MANT_WIDTH + OUT_MXFP_EXP_WIDTH],
                        {OUT_MXFP_EXP_WIDTH{1'b0}},
                        p1_rounded_element[i][OUT_MXFP_MANT_WIDTH - 1 : 0]
                    };
                end else begin
                    element_data_out[i] = {
                        p1_rounded_element[i][OUT_MXFP_MANT_WIDTH + OUT_MXFP_EXP_WIDTH],
                        new_element_exp[OUT_MXFP_EXP_WIDTH-1:0],
                        p1_rounded_element[i][OUT_MXFP_MANT_WIDTH - 1 : 0]
                    };
                end
                scale_data_out = exp_max;
            end
        end    
    endgenerate

endmodule
