`timescale 1ns / 1ps
/*
Module      : Floating Point Configurable Precision Adder (With Sign)
Timing      : Combinatorial Logic
Description : FP_Adder
            - The lossy trade-off in FP adder is between output width and exp_difference.
            - (If the exponent difference is large, maintaining full precision may require a wider output to preserve underflowed bits. We choose truncate(floor) the extra, which is also the lossy part.)
*/

module fp_adder #(
    parameter int IN_EXP_WIDTH = 5,
    parameter int IN_FIX_WIDTH = 10,
    parameter int IN_FIX_FRAC_WIDTH = IN_FIX_WIDTH - 1,
    // Amount of bits needed to shift mantissas for alignment
    parameter int OUT_EXP_WIDTH = -1,
    parameter int OUT_FIX_WIDTH = -1,
    parameter int OUT_FIX_FRAC_WIDTH = -1,
    // PIPELINE_STAGE=1 inserts a register between align (exp_diff + shift)
    // and the mantissa add, breaking the combinational chain.
    // Adds 1 cycle of latency. Callers must pass clk/rst when enabled.
    parameter int PIPELINE_STAGE = 0
)(
    input  logic clk,
    input  logic rst,
    input  logic signed [IN_EXP_WIDTH - 1:0] exp_a,
    input  logic signed [IN_FIX_WIDTH - 1:0] mant_a,
    input  logic signed [IN_EXP_WIDTH - 1:0] exp_b,
    input  logic signed [IN_FIX_WIDTH - 1:0] mant_b,
    output logic signed [OUT_EXP_WIDTH - 1:0] exp_out,
    output logic signed [OUT_FIX_WIDTH - 1:0] mant_out
);

    localparam int DATA_FIX_WIDTH = OUT_FIX_WIDTH - 1;
    localparam int DATA_FIX_FRAC_WIDTH = OUT_FIX_FRAC_WIDTH;
    initial begin
        assert (IN_FIX_FRAC_WIDTH <= OUT_FIX_FRAC_WIDTH)
            else $error("IN_FIX_FRAC_WIDTH must be less than OUT_FIX_FRAC_WIDTH");
    end

    localparam signed FRAC_DIFF = DATA_FIX_FRAC_WIDTH - IN_FIX_FRAC_WIDTH;

    logic signed [IN_EXP_WIDTH - 1:0]   exp_diff;
    logic signed [DATA_FIX_WIDTH - 1:0] mant_a_shifted, mant_b_shifted;
    logic signed [OUT_EXP_WIDTH - 1:0]  exp_out_align;

    always_comb begin
        if (exp_a > exp_b) begin
            exp_diff = exp_a - exp_b;
        end else begin
            exp_diff = exp_b - exp_a;
        end

        if (exp_a > exp_b) begin
            mant_a_shifted = mant_a << FRAC_DIFF;
            mant_b_shifted = (mant_b << FRAC_DIFF) >>> exp_diff;
            exp_out_align = exp_a;
        end else begin
            mant_a_shifted = (mant_a << FRAC_DIFF) >>> exp_diff;
            mant_b_shifted = mant_b << FRAC_DIFF;
            exp_out_align = exp_b;
        end
    end

    // Pipeline boundary between align (shifts) and add (CARRY4).
    logic signed [DATA_FIX_WIDTH - 1:0] mant_a_s1, mant_b_s1;
    logic signed [OUT_EXP_WIDTH - 1:0]  exp_out_s1;
    generate
        if (PIPELINE_STAGE) begin : gen_pipelined
            always_ff @(posedge clk) begin
                if (rst) begin
                    mant_a_s1  <= '0;
                    mant_b_s1  <= '0;
                    exp_out_s1 <= '0;
                end else begin
                    mant_a_s1  <= mant_a_shifted;
                    mant_b_s1  <= mant_b_shifted;
                    exp_out_s1 <= exp_out_align;
                end
            end
        end else begin : gen_comb
            assign mant_a_s1  = mant_a_shifted;
            assign mant_b_s1  = mant_b_shifted;
            assign exp_out_s1 = exp_out_align;
        end
    endgenerate

    assign exp_out = exp_out_s1;
    (* use_dsp = "yes" *) assign  mant_out = mant_a_s1 + mant_b_s1;


endmodule
