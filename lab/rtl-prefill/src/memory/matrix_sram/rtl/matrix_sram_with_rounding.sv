`timescale 1ns/1ps

/*
Module      : Top Level SRAM design solely for Matrix Machine 
Timing      : Sequential Logic, x cycle for read/write process.
Description :
            : This module supports parallel row / column read and write.
            : The addressing mode is Little Endian.
            ：The units for the address is Byte
            : Note the read process take 2 cycles to complete.
Status      : Passed Simple Row/Col Read/Write Tests
*/


module matrix_sram_with_rounding #(
    // MX-FP Data Format
    parameter MXFP_EXP_WIDTH    = 4,
    parameter MXFP_MANT_WIDTH   = 3,
    parameter MXFP_SCALE_WIDTH  = 8,
    parameter WT_MX_INT_ENABLE  = 0,
    parameter WT_MX_INT_WIDTH   = 7,
    parameter INT_DATA_WIDTH    = 32,

    // Dimension
    parameter   MLEN            = 8,
    parameter   BLOCK_DIM       = 4,
    localparam  BLOCK_NUM       = MLEN / BLOCK_DIM,
    // MX-INT vs MXFP element width selection
    localparam  WT_ELEMENT_WIDTH = WT_MX_INT_ENABLE ? WT_MX_INT_WIDTH : (MXFP_EXP_WIDTH + MXFP_MANT_WIDTH + 1),

    // SRAM
    parameter   SRAM_DEPTH      = 128,
    localparam  AddrLen         = $clog2(SRAM_DEPTH),
    parameter   PARALLEL_DIM    = 1,
    parameter   PREFETCH_AMOUNT = 4

) (
    input   logic clk,
    input   logic rst,

    // Read Operation
    input   logic req,
    input   logic transposed_read,
    input   logic [INT_DATA_WIDTH-1:0] sram_raddr,
    output  logic [PARALLEL_DIM - 1 : 0][MLEN - 1 : 0][WT_ELEMENT_WIDTH - 1 : 0]              element_out,
    output  logic [PARALLEL_DIM - 1 : 0][BLOCK_NUM - 1 : 0][MXFP_SCALE_WIDTH - 1 : 0]         scale_out,

    // Write Operation
    input   logic wen,
    output  logic write_response,
    input   logic [INT_DATA_WIDTH-1:0] sram_waddr,
    input   logic [PARALLEL_DIM - 1 : 0][MLEN - 1 : 0][WT_ELEMENT_WIDTH - 1 : 0]              element_in,
    input   logic [PARALLEL_DIM - 1 : 0][BLOCK_NUM - 1 : 0][MXFP_SCALE_WIDTH - 1 : 0]         scale_in,

    // Prefetch Status
    input   logic [INT_DATA_WIDTH - 1 : 0] prefetch_addr,
    input   logic prefetch_en,
    output  logic data_not_ready
);

// -----------------------------
// Address Translation & Prefetch Tag Matching
// -----------------------------

logic [AddrLen - 1 : 0] waddr_for_sub_sram, raddr_for_sub_sram, prefetch_addr_for_sub_sram;
// Bytes per MRAM row — accounts for actual element bit-width (e.g. MXFP4=4bit, MXFP8=8bit, MXINT=7bit).
// Uses WT_ELEMENT_WIDTH which is conditionally set based on WT_MX_INT_ENABLE.
localparam BITWIDTH_PER_ROW = MLEN * PARALLEL_DIM * WT_ELEMENT_WIDTH / 8;
wire [AddrLen + $clog2(BITWIDTH_PER_ROW) - 1 : 0] shifted_prefetch_addr;

assign waddr_for_sub_sram = sram_waddr >> $clog2(BITWIDTH_PER_ROW);
assign raddr_for_sub_sram = sram_raddr >> $clog2(BITWIDTH_PER_ROW);

assign shifted_prefetch_addr = prefetch_addr >> $clog2(BITWIDTH_PER_ROW);
assign prefetch_addr_for_sub_sram = shifted_prefetch_addr[AddrLen-1:0];

// Tag Matching, trackinng the prefetch status.
logic [SRAM_DEPTH - 1 : 0] mem_data_tag;
logic [AddrLen - 1 : 0] raddr_for_tag_lookup;
logic req_for_tag_lookup;

logic wen_delay;
logic [AddrLen - 1 : 0] waddr_for_sub_sram_delay;
always_ff @(posedge clk) begin
    if (rst) begin
        wen_delay <= 1'b0;
        waddr_for_sub_sram_delay <= '0;
        raddr_for_tag_lookup <= '0;
        req_for_tag_lookup <= 1'b0;
    end else begin
        wen_delay <= wen;
        waddr_for_sub_sram_delay <= waddr_for_sub_sram;
        raddr_for_tag_lookup <= raddr_for_sub_sram;
        req_for_tag_lookup <= req;
    end
end


always_ff @(posedge clk) begin
    if (rst) begin
        mem_data_tag <= {{SRAM_DEPTH{1'b1}}};
    end else if (prefetch_en) begin
        for (int i = 0; i < PREFETCH_AMOUNT; i++) begin
            mem_data_tag[prefetch_addr_for_sub_sram + i] <= 1'b0;
        end
    end else if (wen_delay) begin
        mem_data_tag[waddr_for_sub_sram_delay] <= 1'b1;
    end
end

always_ff @(posedge clk) begin
    if (rst) begin
        data_not_ready <= 1'b0;
    end else begin
        data_not_ready <= req_for_tag_lookup & !(&mem_data_tag[raddr_for_tag_lookup +: MLEN]);
    end
end

// -----------------------------
// Memory Storage (Element and Scale)
// -----------------------------

// scale duplication
logic scale_write_response, element_write_response;
logic [PARALLEL_DIM - 1 : 0][MLEN - 1 : 0][MXFP_SCALE_WIDTH - 1 : 0] dumplicated_scale_in;
logic [PARALLEL_DIM - 1 : 0][MLEN - 1 : 0][MXFP_SCALE_WIDTH - 1 : 0] loaded_scale_out;
logic [PARALLEL_DIM - 1 : 0][MLEN - 1 : 0][WT_ELEMENT_WIDTH - 1 : 0] loaded_element_out;

assign write_response = scale_write_response & element_write_response;

duplicate_data_section #(
    .DATA_SEC_WIDTH     (MXFP_SCALE_WIDTH),
    .REPEAT             (BLOCK_DIM),
    .BITSTREAM_WIDTH    (MXFP_SCALE_WIDTH * BLOCK_NUM * PARALLEL_DIM)
) dumplicate_scale(
    .in_data        (scale_in),
    .out_data       (dumplicated_scale_in)
);

// scale storage
biaccess_sram #(
    .DataWidth      (MXFP_SCALE_WIDTH),
    .SRAM_DEPTH     (SRAM_DEPTH),
    .MLEN           (MLEN),
    .Parallel_Rd_Dim(PARALLEL_DIM)
) scale_storage (
    .clk(clk),
    .req(req),
    .transposed_read    (transposed_read),
    .sram_raddr         (raddr_for_sub_sram),
    .out_data           (loaded_scale_out),
    .wen_req            (wen),
    .write_response     (scale_write_response),
    .sram_waddr         (waddr_for_sub_sram),
    .write_data         (dumplicated_scale_in)
);

// element storage (supports both MXFP and MXINT based on WT_MX_INT_ENABLE)
biaccess_sram #(
    .DataWidth      (WT_ELEMENT_WIDTH),
    .SRAM_DEPTH     (SRAM_DEPTH),
    .MLEN           (MLEN),
    .Parallel_Rd_Dim(PARALLEL_DIM)
) element_storage (
    .clk(clk),
    .req(req),
    .transposed_read    (transposed_read),
    .sram_raddr         (raddr_for_sub_sram),
    .out_data           (loaded_element_out),
    .wen_req            (wen),
    .write_response     (element_write_response),
    .sram_waddr         (waddr_for_sub_sram),
    .write_data         (element_in)
);

// -----------------------------
// Output Data with Rescale
// -----------------------------

// Intermediate signals for rescaled data
logic [PARALLEL_DIM - 1 : 0][MLEN - 1 : 0][WT_ELEMENT_WIDTH - 1 : 0] rescaled_element;
logic [PARALLEL_DIM - 1 : 0][BLOCK_NUM - 1 : 0][MXFP_SCALE_WIDTH - 1 : 0] rescaled_scale;

generate
    if (WT_MX_INT_ENABLE) begin : mxint_mode
        // MXINT mode: use mx_int_rescale for each block
        // For row-wise read (no transpose), scales are the same per block - rescale is no-op
        // For column-wise read (transposed), scales vary per element - rescale aligns them
        for (genvar i = 0; i < PARALLEL_DIM; i++) begin : mxint_parallel
            for (genvar j = 0; j < BLOCK_NUM; j++) begin : mxint_block
                mx_int_rescale #(
                    .MXINT_WIDTH        (WT_MX_INT_WIDTH),
                    .MXINT_SCALE_WIDTH  (MXFP_SCALE_WIDTH),
                    .BLOCK_DIM          (BLOCK_DIM)
                ) rescale_output (
                    .clk(clk),
                    .rst(rst),
                    .element_in         (loaded_element_out[i][j * BLOCK_DIM +: BLOCK_DIM]),
                    .scale_in           (loaded_scale_out[i][j * BLOCK_DIM +: BLOCK_DIM]),
                    .element_data_out   (rescaled_element[i][j * BLOCK_DIM +: BLOCK_DIM]),
                    .scale_data_out     (rescaled_scale[i][j])
                );
            end
        end
    end else begin : mxfp_mode
        // MXFP mode: use mx_fp_rescale for each block
        for (genvar i = 0; i < PARALLEL_DIM; i++) begin : output_rescale
            for (genvar j = 0; j < BLOCK_NUM; j++) begin : output_rescale_block
                mx_fp_rescale #(
                    .IN_MXFP_EXP_WIDTH      (MXFP_EXP_WIDTH),
                    .IN_MXFP_MANT_WIDTH     (MXFP_MANT_WIDTH),
                    .MXFP_SCALE_WIDTH       (MXFP_SCALE_WIDTH),
                    .BLOCK_DIM              (BLOCK_DIM),
                    .OUT_MXFP_EXP_WIDTH     (MXFP_EXP_WIDTH),
                    .OUT_MXFP_MANT_WIDTH    (MXFP_MANT_WIDTH)
                ) rescale_output(
                    .clk(clk),
                    .rst(rst),
                    .element_in         (loaded_element_out[i][j * BLOCK_DIM +: BLOCK_DIM]),
                    .scale_in           (loaded_scale_out[i][j * BLOCK_DIM +: BLOCK_DIM]),
                    .element_data_out   (rescaled_element[i][j * BLOCK_DIM +: BLOCK_DIM]),
                    .scale_data_out     (rescaled_scale[i][j])
                );
            end
        end
    end
endgenerate

// -----------------------------
// Output Registration (matching timing model from matrix_sram_without_rounding)
// -----------------------------

logic rd_data_valid;
always_ff @(posedge clk) begin
    if (rst) begin
        element_out <= '0;
        scale_out <= '0;
        rd_data_valid <= 1'b0;
    end else begin
        rd_data_valid <= req;
        if (rd_data_valid) begin
            element_out <= rescaled_element;
            scale_out   <= rescaled_scale;
        end
    end
end

endmodule
