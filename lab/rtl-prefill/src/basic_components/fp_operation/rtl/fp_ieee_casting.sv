`timescale 1ns / 1ps
/*
Module      : fp_ieee_casting
Timing      : 2-stage pipeline (exponent cast -> mantissa cast)
Description : FP_IEEE_Casting
            - Performs casting between IEEE floating-point formats.
            - Only **reducing** bit width is allowed.
            - This operation is **lossy**: For instance, to cast a value like xxxx to x000, we **first truncate/floor** to xxx0, then apply **round-to-nearest-even**.
*/

module fp_ieee_casting #(
    parameter   IN_EXP_WIDTH = 7,
    parameter   IN_MANT_WIDTH = 6,
    parameter   OUT_EXP_WIDTH = 6,
    parameter   OUT_MANT_WIDTH = 4
)(
    input clk,
    input rst,
    input  logic data_in_valid,
    input  logic [IN_EXP_WIDTH + IN_MANT_WIDTH : 0] data_in,  // {sign, exp, mant}
    output logic data_out_valid,
    output logic [OUT_EXP_WIDTH + OUT_MANT_WIDTH : 0] data_out
);
    initial begin
        assert (IN_EXP_WIDTH >= OUT_EXP_WIDTH)
            else $error("IN_EXP_WIDTH must be greater than or equal to OUT_EXP_WIDTH");
        assert (IN_MANT_WIDTH >= OUT_MANT_WIDTH)
            else $error("IN_MANT_WIDTH must be greater than or equal to OUT_MANT_WIDTH");
    end

    logic [OUT_EXP_WIDTH + IN_MANT_WIDTH : 0] exp_casted_data;
    logic [OUT_EXP_WIDTH + IN_MANT_WIDTH : 0] p1_exp_casted_data;
    logic p1_data_valid;
    logic [OUT_EXP_WIDTH + OUT_MANT_WIDTH : 0] intermediate_data_out;

    fp_ieee_exponent_casting #(
        .IN_EXP_WIDTH(IN_EXP_WIDTH),
        .OUT_EXP_WIDTH(OUT_EXP_WIDTH),
        .MANT_WIDTH(IN_MANT_WIDTH)
    ) fp_ieee_exponent_casting_inst (
        .data_in(data_in),
        .data_out(exp_casted_data)
    );

    always_ff @(posedge clk) begin
        if (rst) begin
            p1_exp_casted_data <= '0;
            p1_data_valid <= 1'b0;
        end else begin
            p1_exp_casted_data <= exp_casted_data;
            p1_data_valid <= data_in_valid;
        end
    end

    fp_ieee_mantissa_casting #(
        .EXP_WIDTH(OUT_EXP_WIDTH),
        .IN_MANT_WIDTH(IN_MANT_WIDTH),
        .OUT_MANT_WIDTH(OUT_MANT_WIDTH)
    ) fp_ieee_mantissa_casting_inst (
        .data_in(p1_exp_casted_data),
        .data_out(intermediate_data_out)
    );

    register_slice_wo_hs #(
        .DATA_WIDTH(OUT_EXP_WIDTH + OUT_MANT_WIDTH + 1)
    ) register_slice_inst (
        .clk(clk),
        .rst(rst),
        .data_in        (intermediate_data_out),
        .data_in_valid  (p1_data_valid),
        .data_out       (data_out),
        .data_out_valid (data_out_valid)
    );

endmodule
